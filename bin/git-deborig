#!/usr/bin/perl

# git-deborig -- try to produce Debian orig.tar using git-archive(1)

# Copyright (C) 2016  Sean Whitton <spwhitton@spwhitton.name>
#
# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 3 of the License, or (at
# your option) any later version.
#
# This program is distributed in the hope that it will be useful, but
# WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
# General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program.  If not, see <http://www.gnu.org/licenses/>.

# Prerequisites:

# apt-get install libparse-debianchangelog-perl libgit-wrapper-perl \
#     libdpkg-perl liblist-compare-perl

use strict;
use warnings;
no warnings "experimental::smartmatch";

use Parse::DebianChangelog;
use Git::Wrapper;
use Dpkg::Version;
use List::Compare;

# Sanity check #1
die "pwd doesn't look like a Debian source package in a git repository ..\n"
  unless ( -d ".git" && -e "debian/changelog" );

# Process command line args
die "usage: git deborig [-f] [TAG]\n" unless ( scalar @ARGV < 3);
my $overwrite = 0;
my $user_tag;
foreach my $arg ( @ARGV ) {
    if ( $arg eq "-f" ) {
        $overwrite = 1;
    } else {
        $user_tag = $arg;
    }
}

# Extract source package name and version from d/changelog
my $changelog = Parse::DebianChangelog->init();
$changelog->parse( { infile => "debian/changelog" } );
my $changelog_data = $changelog->dpkg();
my $version = Dpkg::Version->new($changelog_data->{"Version"});
my $upstream_version = $version->version();
my $source = $changelog_data->{"Source"};

# Sanity check #2
die "this looks like a native package .." if $version->is_native();

# Default to gzip
my $compressor = "gzip";
my $compression = "gz";
# Now check if we can use xz
if ( -e "debian/source/format" ) {
    open( my $format_fh, '<', "debian/source/format" )
      or die "couldn't read debian/source/format";
    my $format = <$format_fh>;
    chomp($format) if defined $format;
    if ( $format eq "3.0 (quilt)" ) {
        $compressor = "xz";
        $compression = "xz";
    }
    close $format_fh;
}

my $orig = "../${source}_$upstream_version.tar.$compression";
die "$orig already exists: not overwriting without -f\n"
  if ( -e $orig && ! $overwrite );

# Get available git tags
my $git = Git::Wrapper->new(".");
my @all_tags = $git->tag();

if ( defined $user_tag ) {
    if ( $user_tag ~~ @all_tags ) {
        system "git archive --format=tar $user_tag | $compressor > $orig";
    } else {
        die "the tag $user_tag does not exist in this repository\n";
    }
} else {
    # See which candidate version tags are present in the repo
    my @candidate_tags = ("$upstream_version",
                          "v$upstream_version",
                          "upstream/$upstream_version"
                         );
    my $lc = List::Compare->new(\@all_tags, \@candidate_tags);
    my @version_tags = $lc->get_intersection();

    # If there is only one candidate version tag, we're good to go.
    # Otherwise, let the user know they can tell us which one to use
    if ( scalar @version_tags > 1 ) {
        print "tags ", join(", ", @version_tags), " all exist in this repository\n";
        print "tell me which one you want to make an orig.tar from: git deborig TAG\n";
        exit 1;
    } else {
        my $tag = shift @version_tags;
        system "git archive --format=tar $tag | $compressor > $orig";
    }
}
